import org.gradle.api.artifacts.maven.MavenDeployment

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

defaultTasks 'clean', 'test', 'jar'

group = 'org.icechamps'

sourceCompatibility = 1.6
version = '0.6'
ext.packaging = 'jar'

def isDevBuild
def isCiBuild
def isReleaseBuild

def sonatypeRepositoryUrl

//set build variables based on build type (release, continuous integration, development)
if (hasProperty("release")) {
    isReleaseBuild = true
    sonatypeRepositoryUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
} else if (hasProperty("ci")) {
    isCiBuild = true
    version += "-SNAPSHOT"
    sonatypeRepositoryUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
} else {
    isDevBuild = true
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Lava Java Library', 'Implementation-Version': version
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testCompile 'junit:junit:4.8.2'
    compile 'com.google.guava:guava:r09'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

//********* artifact signing *********
if (isReleaseBuild) {
    signing {
        sign configurations.archives
    }
} else {
    task signArchives {
        // do nothing
    }
}

uploadArchives {
    repositories {
        if (isDevBuild) {
            mavenLocal()
        } else {
            mavenDeployer {
                if (isReleaseBuild) {
                    beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
                }

                repository(url: sonatypeRepositoryUrl) {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }

                pom.project {
                    name "lava"
                    description "A Java library that provides extension methods for collections which allow them to be queried and manipulated easily"
                    packaging 'jar'
                    url 'http://metaldrummer610.github.io/Lava/'

                    scm {
                        url 'http://github.com/metaldrummer610/Lava'
                        connection 'scm:git:http://github.com/metaldrummer610/Lava.git'
                        developerConnection 'scm:git:git@github.com:metaldrummer610/Lava.git'
                    }

                    licenses {
                        license {
                            name 'MIT'
                            url 'http://opensource.org/licenses/MIT'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'metaldrummer610'
                            name 'Robert Diaz'
                            organization 'Icechamps.org'
                            organizationUrl 'http://github.com/metaldrummer610'
                        }
                    }
                }
            }
        }
    }
}
